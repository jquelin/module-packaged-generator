#!/usr/bin/perl

use 5.010;
use strict;
use warnings;

package pkgcpan;
# ABSTRACT: create db of available Perl modules

use File::Spec::Functions qw{ catdir updir };
use Getopt::Long::Descriptive;
use Moose;

# for dev purposes
use FindBin qw{ $Bin };
use lib catdir( $Bin, updir, 'lib' );

use Module::Packaged::Generator;

with 'Module::Packaged::Generator::Role::Loggable';


# -- main program

# parse command-line options
my ($opt, $usage) = describe_options(
    '%c %o',
    [ 'file|f=s'  => "path to the database file"       ],
    [],
    [ 'quiet|q'   => "don't print anything but errors" ],
    [ 'verbose|v' => "print debug messages"            ],
    [ 'help|h'    => "print usage message and exit"    ],
);
print($usage->text), exit if $opt->help;

# update logger behaviour
my $self = pkgcpan->new;
$self->set_debug( $opt->verbose );
$self->set_muted( $opt->quiet );

# run the whole show
my %param;
$param{ file } = $opt->file if $opt->file;
Module::Packaged::Generator->new(\%param)->run;

exit;
__END__

=head1 SYNOPSIS

    $ pkgcpan [options]
    $ pkgcpan --help


=head1 DESCRIPTION

This script will create and populate a sqlite database with the
available Perl modules for your distribution.

